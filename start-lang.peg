// Language Nodes

{
  var handle = require('./start-lib').handle;

  function mixin(object, properties) {
    Object.keys(properties).forEach(function(prop) {
      object[prop] = properties[prop];
    });
  }

  function Node() {}

  Node.extend = function(options) {
    var delegate = options.constructor,
        ctor = function() {
          // grab the info about where we are in the source
          this._text = text();
          this._offset = offset();
          this._line = line();
          this._column = column();

          // copy from proto so we can inspect it
          this._node = this.node;

          delegate.apply(this, arguments);
        };

    ctor.prototype = Object.create(Node.prototype);
    options.constructor = ctor;
    mixin(ctor.prototype, options);
    return ctor;
  };

  var Statements = Node.extend({
    node: 'statements',

    constructor: function(stmts) {
      this.stmts = stmts;
    },

    run: function(ctx) {
      this.stmts.forEach(function(stmt) {
        stmt.run(ctx);
      });
    }
  });

  var IfBlock = Node.extend({
    node: 'if_block',

    constructor: function(cond, tstmts, fstmts) {
      this.cond = cond;
      this.tstmts = tstmts;
      this.fstmts = fstmts;
    },

    run: function(ctx) {
      if (this.cond.evaluate(ctx)) {
        this.tstmts.run(ctx);
      } else if (this.fstmts) {
        this.fstmts.run(ctx);
      }
    }
  });

  var ForBlock = Node.extend({
    node: 'for_block',

    constructor: function(name, range, stmts) {
      this.name = name;
      this.range = range;
      this.stmts = stmts;
    },

    run: function(ctx) {
      var range = this.range.evaluate(ctx);

      handle(range).enumerate(range).forEach(function(i) {
        ctx.set(this.name, i);
        this.stmts.run(ctx);
      }, this);
    }
  });

  var BeginBlock = Node.extend({
    node: 'begin_block',

    constructor: function(name, stmts) {
      this.name = name;
      this.stmts = stmts;
    },

    run: function(ctx) {
      ctx.set(this.name, this.invoke.bind(this, ctx));
    },

    invoke: function(ctx, args) {
      ctx.push();
      try {
        this.stmts.run(ctx);
      } finally {
        ctx.pop();
      }
    }
  });

  var Funcall = Node.extend({
    node: 'funcall',

    constructor: function(target, args) {
      this.target = target;
      this.args = args || [];
    },

    run: function(ctx) {
      this.evaluate(ctx);
    },

    evaluate: function(ctx) {
      return ctx.funcall(
        this.target.evaluate(ctx),
        this.target.node == 'variable' && this.target.name,
        this.args.map(function(arg) {
          return arg.evaluate(ctx);
        }));
    }
  });

  var Variable = Node.extend({
    node: 'variable',

    constructor: function(name, index) {
      this.name = name;
    },

    evaluate: function(ctx) {
      return ctx.get(this.name);
    }
  });

  var Assignment = Node.extend({
    node: 'assignment',

    constructor: function(name, value) {
      this.name = name;
      this.value = value;
    },

    run: function(ctx) {
      ctx.set(this.name, this.value.evaluate(ctx));
    }
  });

  var Index = Node.extend({
    node: 'index',

    constructor: function(base, index) {
      this.base = base;
      this.index = index;
    },

    evaluate: function(ctx) {
      return ctx.getindex(this.base.evaluate(ctx), this.index.evaluate(ctx));
    }
  });

  var IndexAssignment = Node.extend({
    node: 'index_assignment',

    constructor: function(base, index, value) {
      this.base = base;
      this.index = index;
      this.value = value;
    },

    run: function(ctx) {
      ctx.setindex(this.base.evaluate(ctx), this.index.evaluate(ctx), this.value.evaluate(ctx));
    }
  });

  function buildIndex(base, indexes, value) {
    indexes || (indexes = []);

    var next, last = indexes.pop();

    while (next = indexes.shift()) {
      base = new Index(base, next);
    }

    if (value == null) {
      return new Index(base, last);
    } else {
      return new IndexAssignment(base, last, value);
    }
  }

  var Literal = Node.extend({
    node: 'literal',

    constructor: function(value) {
      this.value = value;
    },

    evaluate: function() {
      return this.value;
    }
  });

  var Comment = Node.extend({
    node: 'comment',

    constructor: function(text) {
      this.text = text;
    },

    run: function() {
    }
  });

  var LogicalOp = Node.extend({
    node: 'logical_op',

    constructor: function(op, left, right) {
      this.op = op;
      this.left = left;
      this.right = right;
    },

    evaluate: function(ctx) {
      // logical ops short-circuit
      return logicalOps[this.op](ctx, this.left, this.right);
    }
  });

  var logicalOps = {
    'and': function(ctx, left, right) { return left.evaluate(ctx) && right.evaluate(ctx); },
    'or':  function(ctx, left, right) { return left.evaluate(ctx) || right.evaluate(ctx); },
    'not': function(ctx, left, right) { return ! right.evaluate(ctx); }
  };

  // take a chain of equal-precedence logical exprs and construct a left-folding tree
  function buildLogicalOp(first, rest) {
    var next;

    rest || (rest = []);
    while (next = rest.shift()) {
      first = new LogicalOp(next[0], first, next[1]);
    }

    return first;
  }

  var BinaryOp = Node.extend({
    node: 'binary_op',

    constructor: function(op, left, right) {
      this.op = op;
      this.left = left;
      this.right = right;
    },

    evaluate: function(ctx) {
      return ctx.binaryop(this.op, this.left.evaluate(ctx), this.right.evaluate(ctx));
    }
  });

  // take a chain of equal-precedence binary exprs and construct a left-folding tree
  function buildBinaryOp(first, rest) {
    var next;

    rest || (rest = []);
    while (next = rest.shift()) {
      first = new BinaryOp(next[0], first, next[1]);
    }

    return first;
  }

  var UnaryOp = Node.extend({
    node: 'unary_op',

    constructor: function(op, right) {
      this.op = op;
      this.right = right;
    },

    evaluate: function(ctx) {
      return ctx.unaryop(this.op, this.right.evaluate(ctx));
    }
  });
}

// Grammar

start
  = Statements

// Statements

Statements
  = EOL* stmts:Statement* {
      return new Statements(stmts);
    }

Statement
  = IfBlock
  / ForBlock
  / BeginBlock
  / Assignment
  / CallStatement
  / Comment

IfBlock
  = __ 'if' WB __ cond:Value EOL
    tstmts:Statements
    fstmts:(
      __ 'else' EOL
      s:Statements {
        return s;
      }
    )?
    __ 'end' EOL {
      return new IfBlock(cond, tstmts, fstmts);
    }

ForBlock
  = __ 'for' WB __ sym:Symbol __ 'in' WB __ range:Value EOL
    stmts:Statements
    __ 'end' EOL {
      return new ForBlock(sym, range, stmts);
    }

BeginBlock
  = __ 'begin' WB __ sym:Symbol EOL
    stmts:Statements
    __ 'end' EOL {
      return new BeginBlock(sym, stmts);
    }

Assignment
  = __ 'let' WB __ sym:Symbol
    indexes:(
      __ '[' __ v:Value ']' {
        return v;
      }
    )*
    __ '=' __ value:Value EOL {
      if (indexes.length == 0) {
        return new Assignment(sym, value);
      } else {
        return buildIndex(new Variable(sym), indexes, value);
      }
    }

CallStatement
  = __ target:(
      'call' WB __ e:IndexExpr { return e; }
      / sym:Symbol { return new Variable(sym); }
    )
    args:(
      __ v:Values? __ {
        return v;
      }
    )? EOL {
      return new Funcall(target, args);
    }

Comment
  = __ '--' __ text:$[^\n]* EOL {
    return new Comment(text);
  }

// Values

Values
  = first:Value rest:( __ ',' __ val:Value { return val; } )* {
      return [first].concat(rest);
    }

Value
  = CondExpr
  / AddExpr

// Conditions

CondExpr
  = first:NotExpr rest:( __ op:CondOp __ e:NotExpr { return [op, e]; } )* {
      return buildLogicalOp(first, rest);
    }

CondOp
  = 'and' WB { return 'and'; }
  / 'or'  WB { return 'or';  }

NotExpr
  = 'not' WB __ comp:RelExpr {
      return new LogicalOp('not', null, comp);
    }
  / RelExpr

RelExpr
  = left:AddExpr __ op:RelOp __ right:AddExpr {
      return new BinaryOp(op, left, right);
    }
  / '(' __ cond:CondExpr __ ')' {
      return cond;
    }

RelOp
  = '='
  / '!='
  / '<='
  / '<'
  / '>='
  / '>'

// Arithmetic

AddExpr
  = first:MultExpr rest:( __ op:AddOp __ e:MultExpr { return [op, e]; } )* {
      return buildBinaryOp(first, rest);
    }

AddOp
  = '+'
  / '-'

MultExpr
  = first:UnaryExpr rest:( __ op:MultOp __ e:UnaryExpr { return [op, e]; } )* {
      return buildBinaryOp(first, rest);
    }

MultOp
  = '*'
  / '/'
  / '%'

UnaryExpr
  = op:AddOp __ num:Number {
      // handle +/- number in the parser
      return new Literal(handle(num).unaryops[op](num));
    }
  / op:AddOp __ right:CallExpr {
      return new UnaryOp(op, right);
    }
  / CallExpr

// Invocation

CallExpr
  = target:IndexExpr __ '(' __ args:Values? __ ')' {
      return new Funcall(target, args);
    }
  / IndexExpr

// Index

IndexExpr
  = sym:Symbol indexes:( __ '[' __ v:Value ']' { return v; } )* {
      if (indexes.length == 0) {
        return new Variable(sym);
      } else {
        return buildIndex(new Variable(sym), indexes);
      }
    }
  / PrimaryExpr

PrimaryExpr
  = lit:Literal {
      return new Literal(lit);
    }
  / '(' __ val:Value __ ')' {
      return val;
    }

// Data

Literal
  = None
  / Boolean
  / Number
  / String

None
  = 'none' WB { return null; }

Boolean
  = 'true'  WB { return true; }
  / 'false' WB { return false; }

Number
  = 'infinity' WB { return Infinity; }
  / num:$( Digits ( '.' Digits )? ( [eE] [-+]? Digits )? ) {
      return parseFloat(num);
    }

Digits
  = $[0-9]+

String
  = '"' chars:Char* '"' { return chars.join(''); }

Char
  = '""' { return '"'; }
  / [^"]

Symbol
  = $( !Reserved [a-z_]i [a-z0-9_]i* )

Reserved
  = 'none'     WB
  / 'true'     WB
  / 'false'    WB
  / 'infinity' WB
  / 'if'       WB
  / 'else'     WB
  / 'end'      WB
  / 'for'      WB
  / 'in'       WB
  / 'begin'    WB
  / 'let'      WB
  / 'call'     WB
  / 'and'      WB
  / 'or'       WB
  / 'not'      WB

// Whitespace

WB
  = ![a-z_]i

__
  = [ \t]*

EOL
  = ( __ [\n;] )+
