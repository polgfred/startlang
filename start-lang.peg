// Language Nodes

{
  var handle = require('start-lib').handle;

  function mixin(object, properties) {
    Object.keys(properties).forEach(function(prop) {
      object[prop] = properties[prop];
    });
  }

  function Node() {}

  Node.extend = function(options) {
    var delegate = options.constructor,
        ctor = function() {
          // grab the info about where we are in the source
          this._text = text();
          this._offset = offset();
          this._line = line();
          this._column = column();

          // copy from proto so we can inspect it
          this._node = this.node;

          delegate.apply(this, arguments);
        };

    ctor.prototype = Object.create(Node.prototype);
    options.constructor = ctor;
    mixin(ctor.prototype, options);
    return ctor;
  };

  var BlockNode = Node.extend({
    node: 'BlockNode',

    constructor: function(elems) {
      this.elems = elems || [];
    }
  });

  var IfNode = Node.extend({
    node: 'IfNode',

    constructor: function(cond, tblock, fblock) {
      this.cond = cond;
      this.tblock = tblock;
      this.fblock = fblock;
    }
  });

  var ForNode = Node.extend({
    node: 'ForNode',

    constructor: function(name, range, block) {
      this.name = name;
      this.range = range;
      this.block = block;
    }
  });

  var WhileNode = Node.extend({
    node: 'WhileNode',

    constructor: function(cond, block) {
      this.cond = cond;
      this.block = block;
    }
  });

  var BeginNode = Node.extend({
    node: 'BeginNode',

    constructor: function(name, block) {
      this.name = name;
      this.block = block;
    }
  });

  var FuncallNode = Node.extend({
    node: 'FuncallNode',

    constructor: function(target, args) {
      this.target = target;
      this.args = args || [];
    }
  });

  var BreakNode = Node.extend({
    node: 'BreakNode'
  });

  var NextNode = Node.extend({
    node: 'NextNode'
  });

  var ReturnNode = Node.extend({
    node: 'ReturnNode',

    constructor: function(result) {
      this.result = result;
    }
  });

  var VariableNode = Node.extend({
    node: 'VariableNode',

    constructor: function(name) {
      this.name = name;
    }
  });

  var AssignNode = Node.extend({
    node: 'AssignNode',

    constructor: function(name, value) {
      this.name = name;
      this.value = value;
    }
  });

  var DeleteNode = Node.extend({
    node: 'DeleteNode',

    constructor: function(name) {
      this.name = name;
    }
  });

  var IndexNode = Node.extend({
    node: 'IndexNode',

    constructor: function(base, index) {
      this.base = base;
      this.index = index;
    }
  });

  var AssignIndexNode = Node.extend({
    node: 'AssignIndexNode',

    constructor: function(base, index, value) {
      this.base = base;
      this.index = index;
      this.value = value;
    }
  });

  var DeleteIndexNode = Node.extend({
    node: 'DeleteIndexNode',

    constructor: function(base, index) {
      this.base = base;
      this.index = index;
    }
  });

  // special token to signal buildIndex that we're building a delete_index call
  var $remove = {};

  // take a base, indexes, and (optionally) a value, and construct a left-folding tree
  // that terminates in an index lookup, assignment, or deletion
  function buildIndex(base, indexes, value) {
    indexes || (indexes = []);

    var next, last = indexes.pop();

    while (next = indexes.shift()) {
      base = new IndexNode(base, next);
    }

    if (value === undefined) {
      return new IndexNode(base, last);
    } else if (value === $remove) {
      return new DeleteIndexNode(base, last);
    } else {
      return new AssignIndexNode(base, last, value);
    }
  }

  var LiteralNode = Node.extend({
    node: 'LiteralNode',

    constructor: function(value) {
      this.value = value;
    }
  });

  var CommentNode = Node.extend({
    node: 'CommentNode',

    constructor: function(text) {
      this.text = text;
    }
  });

  var LogicalOpNode = Node.extend({
    node: 'LogicalOpNode',

    constructor: function(op, left, right) {
      this.op = op;
      this.left = left;
      this.right = right;
    }
  });

  // take a chain of equal-precedence logical exprs and construct a left-folding tree
  function buildLogicalOp(first, rest) {
    if (rest.length == 0) {
      return first;
    } else {
      var next = rest.shift();
      return buildLogicalOp(new LogicalOpNode(next[0], first, next[1]), rest);
    }
  }

  var BinaryOpNode = Node.extend({
    node: 'BinaryOpNode',

    constructor: function(op, left, right) {
      this.op = op;
      this.left = left;
      this.right = right;
    }
  });

  // take a chain of equal-precedence binary exprs and construct a left-folding tree
  function buildBinaryOp(first, rest) {
    if (rest.length == 0) {
      return first;
    } else {
      var next = rest.shift();
      return buildBinaryOp(new BinaryOpNode(next[0], first, next[1]), rest);
    }
  }

  // same, but fold right
  function buildBinaryOpRight(rest, last) {
    if (rest.length == 0) {
      return last;
    } else {
      var next = rest.pop();
      return buildBinaryOpRight(rest, new BinaryOpNode(next[0], next[1], last));
    }
  }

  var UnaryOpNode = Node.extend({
    node: 'UnaryOpNode',

    constructor: function(op, right) {
      this.op = op;
      this.right = right;
    }
  });
}

// Grammar

start
  = Block

// Top-level

Block
  = EOL* elems:( __ elem:BlockElement EOL { return elem; } )* {
      return new BlockNode(elems);
    }

BlockElement
  = Control
  / Statement
  / Comment

Control
  = IfElse
  / For
  / While
  / Begin

IfElse
  = __ 'if' WB __ cond:Value EOL
    tblock:Block
    fblock:(
      __ 'else' EOL
      b:Block {
        return b;
      }
    )?
    __ 'end' {
      return new IfNode(cond, tblock, fblock);
    }

For
  = __ 'for' WB __ sym:Symbol __ 'in' WB __ range:Value EOL
    block:Block
    __ 'end' {
      return new ForNode(sym, range, block);
    }

While
  = __ 'while' WB __ cond:Value EOL
    block:Block
    __ 'end' {
      return new WhileNode(cond, block);
    }

Begin
  = __ 'begin' WB __ sym:Symbol EOL
    block:Block
    __ 'end' {
      return new BeginNode(sym, block);
    }

Statement
  = Assign
  / Delete
  / Call
  / Flow

Assign
  = __ 'let' WB __ sym:Symbol
    indexes:(
      __ '[' __ v:Value ']' {
        return v;
      }
    )*
    __ '=' __ value:Value {
      if (indexes.length == 0) {
        return new AssignNode(sym, value);
      } else {
        return buildIndex(new VariableNode(sym), indexes, value);
      }
    }

Delete
  = __ 'delete' WB __ sym:Symbol
    indexes:(
      __ '[' __ v:Value ']' {
        return v;
      }
    )* {
      if (indexes.length == 0) {
        return new DeleteNode(sym);
      } else {
        return buildIndex(new VariableNode(sym), indexes, $remove);
      }
    }

Call
  = __ target:(
      'call' WB __ e:IndexExpr { return e; }
      / sym:Symbol { return new VariableNode(sym); }
    )
    args:(
      __ v:Values? __ {
        return v;
      }
    )? {
      return new FuncallNode(target, args);
    }

Flow
  = __ 'break' {
      return new BreakNode;
    }
  / __ 'next' {
      return new NextNode;
    }
  / __ 'return' result:( WB __ val:Value { return val; } )? {
      return new ReturnNode(result);
    }

Comment
  = __ '--' __ text:$[^\n]* {
      return new CommentNode(text);
    }

// Values

Values
  = first:Value rest:( __ ',' __ val:Value { return val; } )* {
      return [first].concat(rest);
    }

Value
  = CondExpr
  / AddExpr

// Conditions

CondExpr
  = first:NotExpr rest:( __ op:CondOp __ e:NotExpr { return [op, e]; } )* {
      return buildLogicalOp(first, rest);
    }

CondOp
  = 'and' WB { return 'and'; }
  / 'or'  WB { return 'or';  }

NotExpr
  = 'not' WB __ comp:RelExpr {
      return new LogicalOpNode('not', null, comp);
    }
  / RelExpr

RelExpr
  = left:AddExpr __ op:RelOp __ right:AddExpr {
      return new BinaryOpNode(op, left, right);
    }
  / '(' __ cond:CondExpr __ ')' {
      return cond;
    }

RelOp
  = '='
  / '!='
  / '<='
  / '<'
  / '>='
  / '>'

// Arithmetic

AddExpr
  = first:MultExpr rest:( __ op:AddOp __ e:MultExpr { return [op, e]; } )* {
      return buildBinaryOp(first, rest);
    }

AddOp
  = '+'
  / '-'

MultExpr
  = first:PowExpr rest:( __ op:MultOp __ e:PowExpr { return [op, e]; } )* {
      return buildBinaryOp(first, rest);
    }

MultOp
  = '*'
  / '/'
  / '%'

PowExpr
  = rest:(e:UnaryExpr __ op:PowOp __ { return [op, e]; } )* last:UnaryExpr {
      return buildBinaryOpRight(rest, last);
    }

PowOp
  = '^'

UnaryExpr
  = op:AddOp __ num:Number {
      // handle +/- number in the parser
      return new LiteralNode(handle(num).unaryops[op](num));
    }
  / op:AddOp __ right:CallExpr {
      return new UnaryOpNode(op, right);
    }
  / CallExpr

// Invocation

CallExpr
  = target:IndexExpr __ '(' __ args:Values? __ ')' {
      return new FuncallNode(target, args);
    }
  / IndexExpr

// Index

IndexExpr
  = sym:Symbol indexes:( __ '[' __ v:Value ']' { return v; } )* {
      if (indexes.length == 0) {
        return new VariableNode(sym);
      } else {
        return buildIndex(new VariableNode(sym), indexes);
      }
    }
  / PrimaryExpr

PrimaryExpr
  = lit:Literal {
      return new LiteralNode(lit);
    }
  / '(' __ val:Value __ ')' {
      return val;
    }

// Data

Literal
  = None
  / Boolean
  / Number
  / String

None
  = 'none' WB { return null; }

Boolean
  = 'true'  WB { return true; }
  / 'false' WB { return false; }

Number
  = 'infinity' WB { return Infinity; }
  / num:$( Digits ( '.' Digits )? ( [eE] [-+]? Digits )? ) {
      return parseFloat(num);
    }

Digits
  = $[0-9]+

String
  = '"' chars:Char* '"' { return chars.join(''); }

Char
  = '""' { return '"'; }
  / [^"]

Symbol
  = $( !Reserved [a-z_]i [a-z0-9_]i* )

Reserved
  = 'none'     WB
  / 'true'     WB
  / 'false'    WB
  / 'infinity' WB
  / 'if'       WB
  / 'else'     WB
  / 'end'      WB
  / 'for'      WB
  / 'in'       WB
  / 'while'    WB
  / 'begin'    WB
  / 'break'    WB
  / 'next'     WB
  / 'return'   WB
  / 'let'      WB
  / 'delete'   WB
  / 'call'     WB
  / 'and'      WB
  / 'or'       WB
  / 'not'      WB

// Whitespace

WB
  = ![a-z_]i

__
  = [ \t]*

EOL
  = ( __ [\n;] )+
